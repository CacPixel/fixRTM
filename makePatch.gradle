
def buildDir = file("${buildDir}/fixrtm")

def oldRTM = file("${projectDir}/mods/rtm.jar")
def oldNGTLib = file("${projectDir}/mods/ngtlib.jar")
def fixRtmBuiltJar = jar.archivePath

def patchesDir = file("${buildDir}/resources/patches")
def unzippedJarDir = file("$buildDir/unzipped")
def fixRtmOutputDir = file("$buildDir/libs")
def remadeClassesDir = file("$buildDir/remade-classes")
def unzippedNgtsDir = file("$buildDir/unzipped-ngts")

task unzipNgtsDir(type: Copy) {
    from (zipTree(oldRTM) + zipTree(oldNGTLib)) {
        include ("**/*.class")
    }
    into unzippedNgtsDir
}

task remakeClasses(type: RemakeClassWithConstantPool) {
    dependsOn build
    dependsOn unzipNgtsDir

    oldFiles = fileTree(unzippedNgtsDir)
    newFiles = zipTree(fixRtmBuiltJar).matching {
        include ("jp/ngt/**/*.class")
    }
    outTo = remadeClassesDir
}

task makeRtmPatch(type: MakeClassBsdiffPatch) {
    dependsOn remakeClasses
    dependsOn unzipNgtsDir
    dependsOn build

    oldFiles = fileTree(unzippedNgtsDir)
    newFiles = fileTree(remadeClassesDir)

    outTo = patchesDir
    patchPrefix = "com/anatawa12/fixRtm/asm/patches"
}

task unzipFixRTM(type: Copy) {
    dependsOn build

    from (zipTree(fixRtmBuiltJar)){
        exclude ("jp/ngt/**/*")
        exclude ("jp/ngt")
        exclude ("jp")
    }

    destinationDir = unzippedJarDir
}

task makeFixrtmJar(type: Jar) {
    dependsOn build
    dependsOn makeRtmPatch
    dependsOn unzipFixRTM

    from ("src/main/resources") {
        include "mcmod.info"
        expand "version": project.version, "mcversion": project.minecraft.version
    }
    from ("src/main/resources") {
        exclude "mcmod.info"
    }
    from unzippedJarDir
    from patchesDir

    destinationDir = fixRtmOutputDir
    baseName = jar.baseName
    version = project.version

    manifest {
        attributes("FMLCorePlugin" : "com.anatawa12.fixRtm.asm.FixRtmCorePlugin",
                "FMLCorePluginContainsFMLMod": "*",
                'FMLAT': 'fix-rtm_at.cfg',
        )
    }
}
