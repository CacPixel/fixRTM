buildscript {
    ext.kotlin_version = '1.4.20'
    ext.jasm_version = '1.0.7'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.anatawa12.forge:ForgeGradle:2.3-1.0.1"
        classpath("com.anatawa12.jasm:jasm-gradle-plugin:$jasm_version") {
            exclude(group: "org.ow2.asm", module: "asm-debug-all")
        }
    }
}

apply plugin: "com.anatawa12.jasm"

apply plugin: "kotlin"
apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

sourceSets {
    api {
        java {
            srcDirs("src/api/rtm", "src/api/ngtlib")
        }
    }
    main {
        jasm {
            srcDirs("src/main/rtm", "src/main/ngtlib")
        }
        resources {
            srcDirs("src/main/rtmResources", "src/main/ngtlibResources")
        }
    }
}

minecraft {
    version = project.forgeVersion
    runDir = "run"

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = project.mcpVersion
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations{
    shade
    compile.extendsFrom shade
}

repositories {
    jcenter()
}

dependencies {
    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    shade group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.1.0'
    shade ("io.sigpipe:jbsdiff:1.0") {
        exclude group: "org.apache.commons", module: "commons-compress"
    }
    shade "com.anatawa12.sai:sai:0.0.1"

    compileOnly files(file("run/fixrtm-cache/script-compiled-class"))
    compileOnly files(sourceSets.main.jasm.outputDir)
    //compileOnly files(new File(projectDir, "mods/rtm.deobf.jar"),
    //        new File(projectDir, "mods/ngtlib.deobf.jar"))

    // https://mvnrepository.com/artifact/org.twitter4j/twitter4j-core
    apiCompile("org.twitter4j:twitter4j-core:4.0.7")
    // https://mvnrepository.com/artifact/com.github.sarxos/webcam-capture
    apiCompile("com.github.sarxos:webcam-capture:0.3.12")

    // https://mvnrepository.com/artifact/org.twitter4j/twitter4j-core
    compile("org.twitter4j:twitter4j-core:4.0.7")
    // https://mvnrepository.com/artifact/com.github.sarxos/webcam-capture
    compile("com.github.sarxos:webcam-capture:0.3.12")

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

runClient {
    environment("fml.coreMods.load", "com.anatawa12.fixRtm.asm.FixRtmCorePlugin")
    /*
    systemProperties["legacy.debugClassLoading"] = "true"
    systemProperties["legacy.debugClassLoadingSave"] = "true"
    // */
    //*
    if (!project.hasProperty("noLogin") && project.hasProperty("minecraft.login.username") && project.hasProperty("minecraft.login.password"))
        args += ["-username", project.getProperty("minecraft.login.username"), "-password", project.getProperty("minecraft.login.password")]
    // */
}

jar {
    configurations.shade.each {dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
            exclude 'LICENSE.txt'
        }
    }

    manifest {
        attributes("FMLCorePlugin" : "com.anatawa12.fixRtm.asm.FixRtmCorePlugin",
                "FMLCorePluginContainsFMLMod": "*",
                'FMLAT': 'fix-rtm_at.cfg',
        )
    }
}

compileKotlin {
    kotlinOptions {
        //freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

task makeSourceDir {
    doLast {
        new File(buildDir, "sources/main/java").mkdirs()
    }
}
compileJava.dependsOn(makeSourceDir)

test {
    useJUnitPlatform()
}

runClient.inputs.property("always_changed", new Date())

apply from: "./processMods.gradle"
apply from: "./makePatch.gradle"
